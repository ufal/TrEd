#!/usr/bin/perl
# -*- cperl -*-
#
################################################################################
$version='$Revision$ ';
$timestamp="Time-stamp: <2005-03-15 17:53:55 pajas>";
$about=
  "Copyright (c) 2001 by Petr Pajas\n".
  "This software is distributed under GPL - The General Public Licence\n".
  "Full text of the GPL can be found at http://www.gnu.org/copyleft/gpl.html";
$lastupdate=$1 if ($timestamp=~/\<([0-9-: ]+) /);
################################################################################
#
# This is "TrPrint" - a simple tool for printing TrEd trees from
# command line. Nevertheless, TrPrint requires a graphical display and
# the PerlTk library.
#

# Once we got rid of them we call getopt to read all the other options
use Tk;

use Tk::Xrm;
# process the standard arguments for X resources
Tk::CmdLine::SetArguments();

processParam() while ($ARGV[0]=~/^[\+\-][^\-]/);

if ($opt_u) {
  print <<EOH;
Usage:
$0 [-c config-file] [options] files [ [options] files ...]
 or
$0 -u          to show usage
 or
$0 -h          to show help
EOH
  exit;
}

if ($opt_h) {
  print <<EOF;
Usage:
$0 -u|-h    for usage/help

 or

$0 [-c config-file] [options/flags] files [ [options/flags] files ...]

where:

-c <file>  specifies a config-file to be used (overrides
	   ~/.tredrc and all the other files TrPrint would otherwise try to
	   search for).

Options:

-e <encoding> Input encoding (default is iso-8859-2).

-o <file>  Output file prefix (otherwise standard output or command is
           used). If more than one output file is to be created, the
           filename given is used as a filename prefix and numbers
           starting from 1 are appended after optional underscore (see
           -U); thus files named as <filename>_<number>.<extension>
           are created if the <file> was <filename>.<extension>.  All
           previous -p option occurrences are ignored.

-r <range> print trees in given range (if no range is given, all trees
           in the files are printed)

-f <font>  name of a TrueType font used when printing to PDF

-p <cmd>   pipe output to standard input of the given command. Previous
           -o options and -I flags are ignored.

Flags:     (each flag -<flag> has also the +<flag> variant whith opposite
           meaning)

-H         print hidden nodes

-M         maximize print size

-S         print the `sentence' below the tree

-R         disable landscape rotation of wide trees

-C         create color output

-E         create Encapsulated PostScript output (implies -1)

-P         create PDF output

-A         right-to-left display settings (Arabic, Hebrew, etc.)

-1         print each tree to a separate output file. (See -o option for
           details on output files naming.)

-N         use tree numbers as output file suffixes (implies -1)

-U         insert underscore before output file number suffix

-I         enable ImageMagick output filter (implies -E and -1)
EOF
  exit;
}

# in Windows, if HOME not defined, use user's AppData folder instaed
if ($^O eq "MSWin32" and !exists $ENV{HOME}) {
  require Win32::Registry;
  my %shf;
  my $ShellFolders;
  my $shfolders="Software\\Microsoft\\Windows\\CurrentVersion\\Explorer\\Shell Folders";
  $::HKEY_CURRENT_USER->Open($shfolders,$ShellFolders) or die "Cannot read $shfolders $^E\n";
  $ShellFolders->GetValues(\%shf);
  $ENV{HOME} = $shf{AppData}[2];
}

# This is first used to find TrEd::Config.pm module.

#our $inputenc='iso-8859-2';

use FindBin;
if (exists $ENV{TREDHOME}) {
  $libDir=$ENV{TREDHOME};
} elsif (-d "$FindBin::RealBin/tredlib") {
  $libDir="$FindBin::RealBin/tredlib";
} elsif (-d "$FindBin::RealBin/../lib/tredlib") {
  $libDir="$FindBin::RealBin/../lib/tredlib";
} elsif (-d "$FindBin::RealBin/../lib/tred") {
  $libDir="$FindBin::RealBin/../lib/tred";
}
print "Trying $libDir\n" if ($libDir);

if (defined $opt_c) {
  @configFileSearchList=($opt_c); # override any other possible config files
}

# We *must* at least find TrEd::Config module to learn the correct libDir!!.
unshift @INC,$libDir if (-d $libDir);

do {
  my $ld=$libDir;
  require TrEd::Config;
  import TrEd::Config;
  import TrEd::Config qw(&read_config &apply_config &set_default_config_file_search_list);
  $libDir=$ld;
};

$treeViewOpts={
	       drawSentenceInfo  => 0,
	       customColors	  => ['darkgreen','darkblue','darkmagenta',
				      'orange','black','DodgerBlue4',
				      'red','gold','cyan','midnightblue']
	      };

require TrEd::Convert;
import TrEd::Convert;

require TrEd::TreeView;

if (defined $opt_c) {
  @config_file_search_list=($opt_c); # override any other possible config files
} else {
  set_default_config_file_search_list();
}
#$set_user_config=\&setConfig;	# trprint specific configuration

my $configFile=read_config();
$opt_M=$maximizePrintSize;

unless (-d $libDir) {
  print <<'EOL';
 TrPrint couldn't find a tredlib directory. You may need to point
 to its location by the TREDHOME variable or add a correct LibDir
 field to your ~/.tredrc.

EOL
    die "Error: Couldn't find lib-directory.";
}

use Cwd;
unshift @INC,$libDir unless (grep($_ eq $libDir, @INC));

require Fslib;
import Fslib;
import Fslib qw(&Index);

# tree printing module
require TrEd::Print;

#load back-ends
@backends=('FSBackend',ImportBackends(qw(NTREDBackend AG2FS TrXMLBackend TEIXMLBackend CSTS_SGML_SP_Backend StorableBackend)));
$CSTS_SGML_SP_Backend::doctype = "$libDir/csts.doctype";

if ($useCzechLocales) {
  use locale;
  use POSIX qw(locale_h);
  setlocale(LC_COLLATE,"cs_CZ");
  setlocale(LC_NUMERIC,"us_EN");
  setlocale(LANG,"czech");
}

$TrEd::Convert::inputenc=$opt_e if $opt_e;
if ($opt_A) {
  $TrEd::Convert::lefttoright=1;
  $TrEd::Config::valueLineReverseLines=1;
  $TrEd::Config::valueLineAlign='right';
  $main::treeViewOpts->{reverseNodeOrder}=1;
  # align node labels to right for more natural look
  $TrEd::TreeView::DefaultNodeStyle{NodeLabel}=
    [-valign => 'top', -halign => 'right'];
  $TrEd::TreeView::DefaultNodeStyle{Node}=
    [-textalign => 'right'];
}

if ($opt_f) {
  $ttFont = $opt_f;
}

my %pp;				# print options

if ($opt_D) {
  $Fslib::Debug=1;
  $TredDebug=1;
}

startMain();	# print the trees

print STDERR "TrPrint ended.\n";

exit;

############################
############################
############################

# sub setConfig {
#   my $confs=shift;

#   $convert=(exists $confs->{imagemagickconvert}) ? tilde_expand($confs->{imagemagickconvert}) : "convert";
#   print STDERR "Applying configuration.\n" if $tredDebug;
# }


sub prepareFont {
  my ($top,$font,$fn)=@_;
  if ($$font!~/^-/) {
    my @a=split(/\s*,\s*/,$$font);
    my $option,$value;
    my @b;
    foreach (@a) {
      ($option, $value) = split(/\s*:\s*/, $_, 2);
      push @b, "-".$option, $value if ($option=~/^(?:size|weight|family|slant|underline|overstrike)$/);
    }
    eval { $top->fontDelete($fn) };
    $top->fontCreate($fn,@b);
    $$font=$fn;
  }
}

sub prepareFonts {
  my $top=shift;
  prepareFont($top,\$font,'C_normal');
  $treeViewOpts->{font}=$font;
  prepareFont($top,\$vLineFont,'C_vline');
}

sub processParam {
  my $arg;
  my $n;
  my $v;
  $arg=shift @ARGV;
  if ($arg =~ /^([\+\-])([huAUDNIHRTSMEP1C]+)/) {
    # T is not used anymore, but it is accepted for compatibility
    $v=$1;
    foreach (split //,$2) {
      ${"opt_$_"} = $v eq '-';
    }
  } elsif ($arg =~ /^-([cfrope])(.*)$/) {
    $n = $1;
    $v = $2 ne "" ? $2 : shift @ARGV;
    ${"opt_$n"}=$v;
    $opt_o=undef if $n eq 'p';
    $opt_p=undef if $n eq 'o';
  }
}

sub startMain {
  my $file;
  my $outfile;
  my $fsfile;
  my @pl=();
  my $ofile_counter=0;
  my $prev_ofile_base=undef;
  my $ofile_base="";
  my $ofile_suffix;
  my $u;

  my $top = MainWindow->new;
  $top->withdraw();
  my $canvas = $top->Canvas();
  $canvas->pack();

  prepareFonts($top);

  while (@ARGV) {
    processParam while ($ARGV[0]=~/^[\+\-][^\-]/);
    shift @ARGV if $ARGV[0]=~/^[\+\-]\-/;
    $file=shift @ARGV;
    $opt_r='-' unless ($opt_r);
    $fsfile = FSFile->newFSFile($file,$TrEd::Convert::inputenc,@backends);
    if ($fsfile->lastTreeNo<0) {
      print STDERR "error: $file: empty or corrupt!\n";
      undef $fsfile;
      next;
    }
    $u = $opt_U ? "_" : "";
    if ($opt_1 or $opt_E or !defined($opt_p) && ($opt_N or $opt_I)) {
      @pl=TrEd::Print::parse_print_list($fsfile,$opt_r);
      $ofile_base=$opt_o || $file;
      $ofile_base=~s/\.([^.]*)$//;

      $ofile_suffix= $1 ne "" && $opt_o ? $1 : $opt_E ? "eps" : "ps";
      unless ($ofile_base eq $prev_ofile_base) {
	$ofile_counter=0;
	$prev_ofile_base=$ofile_base;
      }
      foreach (@pl) {
	$ofile_counter++ unless $opt_N;
	$outfile= $opt_N ?
	  $ofile_base."$u$_.$ofile_suffix" :
	  $ofile_base."$u".sprintf('%03d',$ofile_counter).".".$ofile_suffix;
	if (defined($opt_p)) {
	  print "Sending $_ of $file to $opt_p\n";
	} else {
	  print "Printing $_ of $file to $outfile\n";
	}
	printThis($fsfile,$top,$canvas,$_,$outfile);
      }
    } else {
      $ofile_base=$opt_o || $file;
      $ofile_base=~s/\.([^.]*)$//;
      $ofile_suffix= $1 ne "" && $opt_o ? $1 : $opt_E ? "eps" : "ps" ;
      $outfile="$ofile_base.$ofile_suffix";
      if (defined($opt_p)) {
	print "Printing $opt_r of $file to $opt_p\n";
      } else {
	print "Printing $opt_r of $file to $outfile\n";
      }
      printThis($fsfile,$top,$canvas,$opt_r,$outfile);
    }
    undef $fsfile;
  }
  $top->destroy;
}

sub printThis {
  my ($fsfile,$top,$c,$range,$outfile)=@_;

  unless ($ttFontFile or !$opt_P) {
    my $fonts=TrEd::Print::get_ttf_fonts(map TrEd::Config::tilde_expand($_),
					 split /,/,$ttFontPath);
    $ttFontFile=$fonts->{$ttFont};
    unless ($ttFontFile) {
      die "Cannot find TrueType font $ttFont in $ttFontPath\n".
	  "You may use one of the following TrueType fonts:\n  ".
	  join("  \n",sort keys %$fonts)."\n";
    }
  }

  TrEd::Print::print_trees($fsfile,
			   undef,
			   $c,
			   $range,
			   !($opt_I||defined($opt_p)),
			   $opt_E || $opt_I,
			   $opt_P,
			   $outfile,
			   $opt_S,
			   defined($opt_p) ?
			   $opt_p : "$imageMagickConvert - $outfile",
#			   $opt_T,
			   $opt_C,
			   $opt_R,
			   $opt_H,
			   { 'PS' => $psFontFile,
			     'AFM' => $psFontAFMFile,
			     'Size' => $psFontSize,
			     'TTF' => $ttFontFile
			   },
			   $prtFmtWidth,
			   $prtHMargin,
			   $prtFmtHeight,
			   $prtVMargin,
			   $opt_M,
			   $psMedia,
			   $treeViewOpts
			  );
}

__END__
